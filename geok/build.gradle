plugins {
    id "org.jetbrains.kotlin.multiplatform"
}

kotlin {
    jvm()

    linuxX64()

    macosX64()
    iosX64()
    iosArm64()

    js {
        browser {
            testTask {
                enabled = false
            }
        }
        nodejs()
    }

    sourceSets {
        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-common"
                implementation "org.jetbrains.kotlin:kotlin-test-annotations-common"
            }
        }
        jvmTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-junit"
            }
        }
        jsTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-js"
            }
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task emptyJavadocs(type: Jar) {
    archiveClassifier.set("javadoc")
}

publishing {
    //MavenCentral requires JavaDoc file
    publications.withType(MavenPublication).all {
        artifact emptyJavadocs
    }
}

apply from: "../ktlint.gradle"
